// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = libs.versions.kotlin.get()
    ext.compose_compiler_version = libs.versions.composecompiler.get()
    ext.compose_version = libs.versions.compose.get()
    ext.androidx_test_version = libs.versions.androidxtest.get()
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath libs.android.gradle.plugin
        classpath libs.maps.secrets.plugin
        classpath libs.kotlin.gradle.plugin
        classpath libs.dokka.plugin
        classpath libs.jacoco.android.plugin
    }
}

plugins {
    alias libs.plugins.dokka apply false
}

ext.projectArtifactId = { project ->
    if (project.name == 'maps-compose' || project.name == 'maps-compose-widgets') {
        return project.name
    } else {
        return null
    }
}

allprojects {
    group = 'com.google.maps.android'
    version = '2.6.0'
    project.ext.artifactId = rootProject.ext.projectArtifactId(project)
}

/**
 * Publishing and signing info
 */
subprojects { project ->
    if (project.ext.artifactId == null) return

    apply plugin: 'com.android.library'
    apply plugin: 'com.hiya.jacoco-android'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'signing'

    // Code coverage
    jacoco {
        toolVersion = libs.versions.jacoco.tool.plugin.get()
    }

    tasks.withType(Test) {
        jacoco.includeNoLocationClasses = true
        jacoco.excludes = ['jdk.internal.*']
    }

    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.source
        archiveClassifier.set("sources")
    }

    task javadocJar(type: Jar) {
        dependsOn(tasks.named("dokkaHtml"))
        dependsOn(tasks.named("dokkaJavadoc"))
        archiveClassifier.set("javadoc")
        from new File(buildDir, "dokka/javadoc")
    }

    publishing {
        publications {
            aar(MavenPublication) {
                groupId project.group
                artifactId project.ext.artifactId
                version project.version

                pom {
                    name = project.name
                    description = "Jetpack Compose components for the Maps SDK for Android"
                    url = "https://github.com/googlemaps/android-maps-compose"
                    scm {
                        connection = 'scm:git@github.com:googlemaps/android-maps-compose.git'
                        developerConnection = 'scm:git@github.com:googlemaps/android-maps-compose.git'
                        url = 'https://github.com/googlemaps/android-maps-compose'
                    }

                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }

                    organization {
                        name = 'Google Inc'
                        url = 'http://developers.google.com/maps'
                    }

                    developers {
                        developer {
                            name = 'Google Inc.'
                        }
                    }
                }

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    project.configurations.api.allDependencies.each { dependency ->
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }

                afterEvaluate {
                    artifact "$buildDir/outputs/aar/$project.name-release.aar"
                    artifact javadocJar
                    artifact sourcesJar
                }
            }
        }

        repositories {
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                name = "mavencentral"
                url = project.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.aar
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
