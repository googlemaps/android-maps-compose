plugins {
    id 'kotlin-android'
}

android {
    namespace "com.google.maps.android.compose"
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.composecompiler.get()
    }

    buildFeatures {
        buildConfig false
        compose true
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-Xexplicit-api=strict'
        freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'

        freeCompilerArgs += [
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:stabilityConfigurationPath=" +
                        layout.projectDirectory.file('compose_compiler.conf').asFile.absolutePath
        ]

        if (findProperty("composeCompilerReports") == "true") {
            freeCompilerArgs += [
                    "-P",
                    "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                            project.buildDir.absolutePath + "/compose_compiler"
            ]
        }
        if (findProperty("composeCompilerMetrics") == "true") {
            freeCompilerArgs += [
                    "-P",
                    "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                            project.buildDir.absolutePath + "/compose_compiler"
            ]
        }
    }
}

dependencies {
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.core
    implementation libs.androidx.compose.foundation
    implementation libs.kotlin
    api libs.maps.ktx.std

    testImplementation libs.test.junit

    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.test.espresso
    androidTestImplementation libs.androidx.test.junit.ktx
}
